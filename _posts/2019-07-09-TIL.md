---
layout: post
title:  "[TIL] 2019-07-08"
date:   2019-07-08 23:00:00
author: Seong-Kyu Yun
categories: [Today I Learned]

---

---
 # Today I Learned (2019.07.08)





## ● Oracle DataBase

---

#### 1.  PL/SQL

---

---

##### 1-1.  예외 처리

---

> 예외는 프로그램 실행 중에 발생한 PL/SQL 오류를 말하는데, 예외는 다음과 같이 발생한다.
>
>   - Oracle 서버에서 묵시적으로 발생
>   - 프로그램에 의해 명시적으로 발생
>
> 이를 처리하는 것을 예외 처리라 하며, 다음과 같이 처리한다.
>
>   - 처리기로 트랩
>   - 호출 환경으로 전달



- [예제 - 정의된 오라클 SERVER 에러 예외 처리]

```sql
BEGIN
UPDATE EMPLOYEE
SET EMP_ID = '&사번' WHERE EMP_ID = 200;
EXCEPTION
WHEN DUP_VAL_ON_INDEX THEN
DBMS_OUTPUT.PUT_LINE('이미 존재하는 사번입니다.');
END;
/
```

- [출력화면]

```code
이미 존재하는 사번입니다.

PL/SQL 프로시저가 성공적으로 완료되었습니다.
```



- [예제 - 미리 정의되지 않은 오라클 SERVER 에러 예외 처리]

```sql
DECLARE
DUP_EMPNO EXCEPTION;
PRAGMA EXCEPTION_INIT(DUP_EMPNO, -00001);
BEGIN
UPDATE EMPLOYEE
SET EMP_ID = '&사번' WHERE EMP_ID = 200;
EXCEPTION
WHEN DUP_EMPNO THEN
DBMS_OUTPUT.PUT_LINE('이미 존재하는 사번입니다.');
END;
/
```

- [출력화면]

```code
DUP_EMPID가 작동되었습니다.

PL/SQL 프로시저가 성공적으로 완료되었습니다.
```



---

#### 2.  PROCEDURE & FUNCTION

---



---

##### 2-1.  PROCEDURE 

---

> PL/SQL문을 저장하는 객체이다. 필요할 때마다 복잡한 구문을 다시 입력 할
> 필요 없이 간단하게 호출해서 실행결과를 얻을 수 있다.

- [사용법]

```CODE
CREATE PROCEDURE 프로시저명
IS PL/SQL 구문
```

- [예제 - 기본 프로시저]

```SQL
CREATE PROCEDURE DEL_EMP
IS 
    BEGIN
        DELETE FROM EMP_COPY;
        COMMIT;
    END;
    /

--------------------------
/*PROCEDURE 실행*/
EXEC DEL_EMP;
```

 

- [등록된 프로시저 조회]

```SQL
SELECT * FROM USER_PROCEDURES;
```



- [예제 - 매개변수가 있는 프로시저]

```sql
CREATE PROCEDURE DEL_EMP_ID(V_ID EMPLOYEE.EMP_ID%TYPE)
IS
    BEGIN 
        DELETE FROM EMP_COPY WHERE EMP_ID=V_ID;
    END;
    /


EXEC DEL_EMP_ID('&사원번호');
```



- [예제 - IN/OUT 매개변수 있는 프로시저]

> 프로시저의 실행결과를 변수에 넣어서 외부로 데이터를 보낼 수 있다.

```sql
CREATE OR REPLACE PROCEDURE SEL_EMP(
    V_EMP_ID IN EMPLOYEE.EMP_ID%TYPE, -- 외부에서 받는 값(IN)
    V_SALARY OUT EMPLOYEE.SALARY%TYPE, -- 외부로 내보내는 값(OUT)
    V_BONUS OUT EMPLOYEE.BONUS%TYPE)
IS 
    BEGIN 
        SELECT SALARY, BONUS
        INTO V_SALARY, V_BONUS
        FROM EMPLOYEE
        WHERE EMP_ID=V_EMP_ID;
    END;
    / 

-- OUT으로 데이터를 받은 변수를 생성
VARIABLE V_SALARY NUMBER;
VARIABLE V_BONUS NUMBER;
-- 변수 출력 테스트(생성이 되었나)
PRINT V_SALARY;
PRINT V_BONUS;
-- 프로시저 실행
EXEC SEL_EMP('&사원번호', :V_SALARY, :V_BONUS);
-- 다시 출력해보면 값이 들어간 것을 확인 가능.
PRINT V_SALARY;
PRINT V_BONUS;
```

- [출력화면 비교]

```CODE
/*프로시저 실행 전*/
   V_SALARY
----------



   V_BONUS
----------


/*프로시저 실행 후*/

  V_SALARY
----------
   8000000


   V_BONUS
----------
       0.3
       
```





---

##### 2-2.  FUNCTION

---

>프로시져와 거의 유사한 용도로 사용하지만, 실행 결과를 되돌려 받을 수
>있는 점에서 프로시저와 다르다.

- [사용법]

```CODE
CREATE OR REPLACE FUNCTION 함수명(매개변수)
RETURN 자료형
IS PL/SQL문
```

- [예제]

```sql
CREATE OR REPLACE FUNCTION 
    SAL_CAL(V_EMP_ID EMPLOYEE.EMP_ID%TYPE)
RETURN NUMBER
IS
    V_SALARY NUMBER;
    V_BONUS NUMBER;
    V_RESULT NUMBER;
    BEGIN 
        SELECT SALARY, NVL(BONUS,0)
        INTO V_SALARY, V_BONUS
        FROM EMPLOYEE
        WHERE EMP_ID = V_EMP_ID;
    V_RESULT := (V_SALARY+(V_SALARY*V_BONUS))*12;
    RETURN V_RESULT;
    END;
    /

SELECT SAL_CAL('&EMP') FROM DUAL;
```

- [사용 예제]

```sql
SELECT * FROM 
    (SELECT (SALARY+(SALARY*NVL(BONUS,0)))*12
        AS TOTAL, A.*
        FROM EMPLOYEE A)
WHERE TOTAL > SAL_CAL('&EMP');
```

---

##### 2-3.  CURSOR

---

>처리 결과가 여러 개의 행으로 구해지는 SELECT문을 처리하기 위해 처리
>결과를 저장해 놓은 객체이다. 
> : CURSOR~ OPEN~ FETCH~ CLOSE 단계로 진행된다.

- [사용법]
```CODE
CURSOR 변수명 IS SELECT ... ;
PL/SQL안에서 OPEN 커서변수명 FETCH 커서변수명 INTO 변수명, 변수명
커서는 반복문 안에서 실행
반복문을 중단하기 위해 EXIT THEN %커서상태 사용
종료가 되면 커서를 받아야 함 CLOSE CUSOR 변수명
```

- [예제]

> 사용법만 보면 어지러울테니 예제를 확인하자.

```sql
DECLARE 
    V_EMP_NAME EMPLOYEE.EMP_NAME%TYPE;
    V_EMP_SALARY EMPLOYEE.SALARY%TYPE;
    CURSOR C1 -- 커서 선언
    IS SELECT EMP_NAME, SALARY FROM EMPLOYEE;
BEGIN 
    OPEN C1; -- 커서 오픈
    LOOP -- 반복문 안에서 커서 실행
    FETCH C1 INTO V_EMP_NAME, V_EMP_SALARY;
    
    EXIT WHEN C1%NOTFOUND; -- 커서상태로 반복문 중단
    
    DBMS_OUTPUT.PUT_LINE(V_EMP_NAME||V_EMP_SALARY);
    END LOOP; 
    CLOSE C1; -- 커서 닫기
END;
/
```

- [예제 - FOR문 이용]
>FOR IN LOOP를 이용하면 LOOP 반복시 자동으로 CURSOR를 OPEN하고 행을 인출(FETCH)한다. LOOP 종료시 자동으로 CURSOR를 CLOSE한다.
```sql
DECLARE 
    V_EMP_NAME EMPLOYEE.EMP_NAME%TYPE;
    V_EMP_SALARY EMPLOYEE.SALARY%TYPE;
    V_EMP EMPLOYEE%ROWTYPE;
    CURSOR C1
    IS SELECT * FROM EMPLOYEE;
BEGIN 
    --FOR문을 이용해서 처리하기
    FOR V_EMP IN C1 LOOP -- 알아서 커서를 열고 닫는다.
    DBMS_OUTPUT.PUT_LINE(V_EMP.EMP_NAME||V_EMP.SALARY);
    END LOOP;
END;
/
```

- [예제 - 선언 생략]

>  FOR IN 문을 사용하면 커서의 선언도 생략할 수 있다.

```sql
BEGIN 
    FOR K IN(SELECT * FROM (EMPLOYEE)) LOOP
        DBMS_OUTPUT.PUT_LINE(K.EMP_NAME || K.SALARY);
    END LOOP;
END;
/
```

**:  위 세 가지 예제는 같은 실행 결과를 가진다.**

- [출력 화면]

```CODE
선동일8000000
송종기6000000
노옹철3700000
송은희2800000
유재식3400000
정중하3900000
 ....(생략)


PL/SQL 프로시저가 성공적으로 완료되었습니다.
```





---

#### 3.  TRIGGER

---

>테이블이나 뷰가 INSERT, UPDATE, DELETE등의 DML문에 의해 변경 될
>경우 자동으로 실행될 내용을 정의하여 저장한 객체이다.

- [사용법]

```CODE
CREATE TRIGGER 트리거명칭 
BEFORE|AFTER DML명령어 ON 대상테이블
FOR EACH ROW
수행될 PL/SQL 구문
```

- [예제]

```sql
CREATE OR REPLACE TRIGGER TG_EMP
AFTER INSERT ON EMPLOYEE
FOR EACH ROW 
BEGIN 
    DBMS_OUTPUT.PUT_LINE('신입이 왔다!');
END;
/
/* 사원 추가 */
INSERT INTO EMPLOYEE VALUES(999,'이태영','920814-1234123','SUN@KH'
    ,'01012341234','D2','J2','S4',100,0.2,200,SYSDATE,NULL,DEFAULT);
```

- [출력화면]

```CODE
신입이 왔다!


1 행 이(가) 삽입되었습니다.
```
